module templatelang

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  libstratego-sglr
  include/TemplateLang
  include/libtemplatelang
  lib/EditorService
  lib/editor-common.generated
imports
  analyze

rules // Main editor interface (defined by editor/TemplateLang-Builders and -References.esv)

  // Analyzes the current program, returning a tuple with errors, warnings, and notes;
  // each a list of (term, message) tuples or simply (message) terms.
  editor-analyze:
    (ast, path, project-path) -> (ast', [], [], [])
    with
      editor-init;
      ast' := <analyze> ast

  // Prints the abstract syntax ATerm of a selection.
  generate-aterm(s):
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path;
      result   := <s> selected

  generate-aterm = generate-aterm(id)

  // Only the builder options are different.
  generate-desugared-aterm = generate-aterm

  // Expanded templates (input to completion template generator)
  generate-expanded =
    generate-aterm(
      collect-om(?Desugared(_));
      map(expand-multi-line-template-production)
    )

  // Generate completion templates
  generate-esv =
    collect-om(?Desugared(_));
    filter(template-to-completion-template);
    !Module("example", NoImports(), [Completions("", <id>)]);
    topdown(try(pp-fix-string-quotes))

  generate-esv-abstract =
    generate-aterm(generate-esv)

  generate-esv-concrete:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"generated.esv")> path;
      result   := <generate-esv; pp-descriptor-to-string> selected

  // Generate SDF
  generate-sdf =
    collect-om(?Desugared(_));
    map(template-production-to-sdf);
    ![context-free-syntax(<id>)];
    add-contentcomplete;
    add-lexical-restrictions;
    !'module(unparameterized("example"), [], [exports(<list-to-conc-grammars>)])

  generate-sdf-abstract =
    generate-aterm(generate-sdf)

  generate-sdf-concrete:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"generated.sdf")> path;
      result   := <generate-sdf; pp-sdf-to-string> selected

  // Generate pretty printer (BOX-based)
  generate-box-pp-string =
    collect-om(?Desugared(_));
    <template-sections-to-stratego-string> [TemplateSection(<id>)]

  generate-box-pp-abstract =
    generate-aterm(
      collect-om(?Desugared(_));
      <template-sections-to-stratego> [TemplateSection(<id>)]
    )

  generate-box-pp-concrete:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"generated.str")> path;
      result   := <generate-box-pp-string> selected

  // On-save handler
  generate-all:
    (selected, position, ast, path, project-path) -> None()
    with
      generate-esv-concrete; write-string-to-file
    with
      generate-sdf-concrete; write-string-to-file
    with
      generate-box-pp-concrete; write-string-to-file

  // When given a tuple (path, string) this writes string into path.
  write-string-to-file =
    with(
      ?(filename, string);
      handle := <fopen> (filename, "w");
      <fputs> (string, handle);
      <fclose> handle;
      <refresh-workspace-file> filename
    )
