module priority/Priority

imports kernel/Kernel constants/NatCon
 
context-free syntax

Associativity.Left = <left>
Associativity.Right = <right>
Associativity.NonAssoc = <non-assoc>
Associativity.Assoc = <assoc>

Attribute.Bracket = <bracket>
Attribute.Assoc = <<Associativity>>

ArgumentIndicator.Default = <\< <arguments:NatCon+; separator=","> \>>
Group.WithArguments = <<Group> <ArgumentIndicator>> {non-assoc}
Group.NonTransitive = <<Group>.> {non-assoc}

Group.SimpleGroup = <<Production>>
Group.ProdsGroup = <{<Productions>}>
Group.AssocGroup = <{<Associativity> : <Productions>}> 

Priority.Chain = <<list:Group+; separator=">">>
//"left":Group Associativity "right":Group             -> Priority  {cons("assoc")}
Priority.Assoc = <<Group> <Associativity> <Group>>

Priorities = <<list:Priority*; separator=",">>
Grammar.Priorities = <priorities <Priorities>>