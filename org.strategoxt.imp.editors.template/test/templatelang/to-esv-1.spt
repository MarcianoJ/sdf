module templatelang-to-esv

language TemplateLang
target language EditorService

setup [[
  templates
]]

target setup [[
  module example
  completions
]]

test empty [[ X.Y = <> ]]
build generate-esv-abstract to [[ ]]

test placeholder [[ X.Y = <foo <Z> bar> ]]
build generate-esv-abstract to [[
  completion template X: "foo Z bar" = "foo " <Z:Z> " bar"
]]

test placeholder (text) [[ X.Y = <foo <Z; text=" display text "> bar> ]]
build generate-esv-abstract to [[
  completion template X: "foo display text bar" = "foo " < display text :Z> " bar"
]]

test placeholder (option) [[ X.Y = <foo <Z?> bar> ]]
build generate-esv-abstract to [[
  completion template X: "foo bar" = "foo " <:Z> " bar"
]]

test placeholder (plus) [[ X.Y = <foo <Z+> bar> ]]
build generate-esv-abstract to [[
  completion template X: "foo Z bar" = "foo " <Z:Z> " bar"
]]

test placeholder (star) [[ X.Y = <foo <Z*> bar> ]]
build generate-esv-abstract to [[
  completion template X: "foo bar" = "foo " <:Z> " bar"
]]

test n placeholders => one (cursor)-directive [[
  X.Y = <
    foo
      <A?>
      <B?>
      <C?>
    bar
  >
]]
build generate-esv-abstract to [[
  completion template X: "foo bar" = "foo\n\t" (cursor) "\nbar" (blank)
]]

test if then statement [[
  Statement.IfThen = <
    <MetaAnno*; separator="\n", hide>
    if <Exp> then
      <Statement*; separator="\n">
    end
  >
]]
build generate-esv-abstract to [[
  completion template Statement: "if Exp then end" =
    "if " <Exp:Exp> " then\n\t" (cursor) "\nend" (blank)
]]
