module to-sig
language TemplateLang
target language Stratego-Sugar

setup [[
  templates
]]

target setup [[
  specification
  signature
    constructors
]]

test OpDecl [[ X.Y = <<A> <B> <C>> ]]
build generate-signature-abstract
to [[ Y : A * B * C -> X ]]

test OpDecl, no-args [[ X.Y = <foo> ]]
build generate-signature-abstract
to [[ Y : X ]]

test OpDeclInj [[ X = <<A>> ]]
build generate-signature-abstract
to [[ : A -> X ]]

test OpDeclInj, no-args [[ X = <foo> ]]
build generate-signature-abstract
to [[ : X ]]

test OpDecl, List [[ X.Y = <<A*> <B+>> ]]
build generate-signature-abstract
to [[ Y : List(A) * List(B) -> X ]]

test OpDecl, Option [[ X.Y = <<A?>> ]]
build generate-signature-abstract
to [[ Y : Option(A) -> X ]]

test OpDecl, multi line template [[
  X.Y = <
    <A>
    <B>
    <C>
  >
]]
build generate-signature-abstract
to [[ Y : A * B * C -> X ]]
